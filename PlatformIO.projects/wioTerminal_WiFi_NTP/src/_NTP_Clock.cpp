/****************************************************************************************************************************
  NINA_TZ_NTP_Clock.ino
  
  For AVR, ESP8266/ESP32, SAMD21/SAMD51, nRF52, STM32, SAM DUE boards using 
  a) Ethernet W5x00, ENC28J60, LAN8742A
  b) WiFiNINA
  c) ESP8266/ESP32 WiFi
  d) ESP8266/ESP32-AT-command WiFi

  Based on and modified from Arduino NTPClient Library (https://github.com/arduino-libraries/NTPClient)
  to support other boards such as ESP8266/ESP32, SAMD21, SAMD51, Adafruit's nRF52 boards, SAM DUE, etc.
  and Ethernet/WiFi/WiFiNINA shields
  
  Copyright (C) 2015 by Fabrice Weinberg and licensed under MIT License (MIT)

  Built by Khoi Hoang https://github.com/khoih-prog/NTPClient_Generic
  Licensed under MIT license
  Version: 3.2.2

  Version Modified By  Date      Comments
  ------- -----------  ---------- -----------
  3.2.1   K Hoang      27/10/2020 Initial porting to support SAM DUE, SAMD21, SAMD51, nRF52, ESP32/ESP8266, STM32, etc. boards 
                                  using Ethernet/WiFi/WiFiNINA shields. Add more features and functions.
  3.2.2   K Hoang      28/10/2020 Add examples to use STM32 Built-In RTC.
 *****************************************************************************************************************************/
#include <Arduino.h>

#include "defines.h"

//////////////////////////////////////////

#include <Timezone_Generic.h>  // https://github.com/khoih-prog/Timezone_Generic

// US Eastern Time Zone (New York, Detroit)
//TimeChangeRule myDST = {"EDT", Second, Sun, Mar, 2, -240};    // Daylight time = UTC - 4 hours
//TimeChangeRule mySTD = {"EST", First, Sun, Nov, 2, -300};     // Standard time = UTC - 5 hours
TimeChangeRule myDST = {"TST", Second, Sun, Mar, 2, 480};  // Daylight time = UTC + 8 hours
TimeChangeRule mySTD = {"TST", First, Sun, Nov, 2, 480};   // Standard time = UTC + 8 hours

Timezone myTZ(myDST, mySTD);

// If TimeChangeRules are already stored in EEPROM, comment out the three
// lines above and uncomment the line below.
//Timezone myTZ(100);       // assumes rules stored at EEPROM address 100

TimeChangeRule *tcr;  // pointer to the time change rule, use to get TZ abbrev

//////////////////////////////////////////

#include <NTPClient_Generic.h>

int status = WL_IDLE_STATUS;  // the Wifi radio's status

// A UDP instance to let us send and receive packets over UDP
WiFiUDP ntpUDP;

// NTP server
//World
char timeServer[] = "time.nist.gov";
// Canada
//char timeServer[] = "0.ca.pool.ntp.org";
//char timeServer[] = "1.ca.pool.ntp.org";
//char timeServer[] = "2.ca.pool.ntp.org";
//char timeServer[] = "3.ca.pool.ntp.org";
// Europe
//char timeServer[] = ""europe.pool.ntp.org";

#define TIME_ZONE_OFFSET_HRS (8)
#define NTP_UPDATE_INTERVAL_MS 60000L

// You can specify the time server pool and the offset (in seconds, can be
// changed later with setTimeOffset() ). Additionaly you can specify the
// update interval (in milliseconds, can be changed using setUpdateInterval() ).
NTPClient timeClient(ntpUDP, timeServer, (3600 * TIME_ZONE_OFFSET_HRS), NTP_UPDATE_INTERVAL_MS);

static bool gotCurrentTime = false;

// format and print a time_t value, with a time zone appended.
void printDateTime(time_t t, const char *tz) {
    char buf[32];
    char m[4];  // temporary storage for month string (DateStrings.cpp uses shared buffer)
    strcpy(m, monthShortStr(month(t)));
    sprintf(buf, "%.2d:%.2d:%.2d %s %.2d %s %d %s",
            hour(t), minute(t), second(t), dayShortStr(weekday(t)), day(t), m, year(t), tz);
    Serial.println(buf);
}

void update_Time(void) {
    // Just get the correct time once
    if (timeClient.updated()) {
        Serial.println("********UPDATED********");

        // set the system time to UTC
        // warning: assumes that compileTime() returns US EDT
        // adjust the following line accordingly if you're in another time zone
        setTime(timeClient.getUTCEpochTime());

        gotCurrentTime = true;
    }
}

void displayClock(void) {
    time_t utc = now();

    Serial.println("============================");

    time_t local = myTZ.toLocal(utc, &tcr);

    printDateTime(utc, "UTC");
    printDateTime(local, tcr->abbrev);
}

void check_status(void) {
    // Update first time after 5s
    static ulong checkstatus_timeout = 5000L;
    static ulong TimeDisplay_timeout = 0;

    static ulong current_millis;

// Display every 10s
#define TIME_DISPLAY_INTERVAL (10000L)

// Update RTC every hour if got correct time from NTP
#define TIME_UPDATE_INTERVAL (3600 * 1000L)

// Retry updating RTC every 5s if haven't got correct time from NTP
#define TIME_RETRY_INTERVAL (3 * 1000L)

    current_millis = millis();

    if ((current_millis > TimeDisplay_timeout) || (TimeDisplay_timeout == 0)) {
        if (gotCurrentTime)
            displayClock();

        TimeDisplay_timeout = current_millis + TIME_DISPLAY_INTERVAL;
    }

    // Update Time every TIME_UPDATE_INTERVAL (3600) seconds.
    if ((current_millis > checkstatus_timeout)) {
        update_Time();

        if (gotCurrentTime) {
            Serial.println("Time updated. Next update in seconds : " + String(TIME_UPDATE_INTERVAL / 1000, DEC));
            checkstatus_timeout = current_millis + TIME_UPDATE_INTERVAL;
        } else {
            Serial.println("Retry Time update in seconds : " + String(TIME_RETRY_INTERVAL / 1000, DEC));
            checkstatus_timeout = current_millis + TIME_RETRY_INTERVAL;
        }
    }
}

//////////////////////////////////////////
extern void checkSdLaunch();
extern void setupTFT();
extern void drawClockHands(time_t t);
extern void drawClockDigits(time_t t);
extern void drawClockFace();

void setup() {
    checkSdLaunch();
    Serial.begin(115200);
    //while (!Serial);
    delay(100);
    Serial.print("\nStarting NINA_TZ_NTP_Clock on " + String(BOARD_NAME));
    Serial.println(" with " + String(SHIELD_TYPE));

    setupTFT();
    // attempt to connect to WiFi network
    while (status != WL_CONNECTED) {
        Serial.print(("Connecting to WPA SSID: "));
        Serial.println(ssid);
        // Connect to WPA/WPA2 network
        status = WiFi.begin(ssid, pass);
    }

    // you're connected now, so print out the data
    Serial.print(F("You're connected to the network, IP = "));
    Serial.println(WiFi.localIP());

    timeClient.begin();
}

extern bool isInitClock;
extern void letItSnow();
void loop() {
    timeClient.update();
    check_status();
    if (gotCurrentTime) {
        int mils = millis();
        if ((mils % 500) == 0) {
            letItSnow();  //Let It Snow!
            if ((mils % 1000) == 0) {
                time_t utc = now();
                time_t local = myTZ.toLocal(utc, &tcr);
                drawClockDigits(local);
                drawClockHands(local);
                isInitClock = 0;
                if ((mils%60000)==0) { //redraw face in 1 min.
                    drawClockFace();
                    isInitClock = 1;
                    }
            }
        }
    }
}
